plugins {
    id "org.jetbrains.intellij" version "1.16.0"
}

group = "us.marseilles"
version = pluginVersion
buildSearchableOptions.enabled = false

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation          group: 'one.util',          name: 'streamex',      version: '0.8.2'
    compileOnly             group: 'org.projectlombok', name: 'lombok',        version: lombokVersion
    annotationProcessor     group: 'org.projectlombok', name: 'lombok',        version: lombokVersion

    testCompileOnly         group: 'org.projectlombok', name: 'lombok',        version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok',        version: lombokVersion
    testImplementation      group: 'org.hamcrest',      name: 'hamcrest-core', version: '2.2'
    testImplementation      group: 'org.mockito',       name: 'mockito-core',  version: '2.1.0'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = ideaVersion

    updateSinceUntilBuild = false // disable building for each major version

    plugins = [
        "java",
        "properties",
        "PsiViewer:${psiViewerPluginVersion}"
    ]
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

patchPluginXml {
    version           = pluginVersion
    sinceBuild        = psiViewerPluginVersion
    untilBuild        = ""
    changeNotes       = htmlFixer("src/main/resources/META-INF/change-notes.html")
    pluginDescription = htmlFixer("src/main/resources/META-INF/description.html")
}